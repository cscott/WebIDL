/**
 * Wikipeg grammar for reading WebIDL
 * 2021-01-27 C. Scott Ananian <cananian@wikimedia.org>
 */
{
/* File-scope initializer */
namespace Wikimedia\WebIDL;
}
{
	/** @var string */
	private $filename = '';
	/** @var int */
	private $lineNum = 1;

	/**
	 * @param string $filename
	 * @param string $contents
	 * @return array
	 */
	public static function load( string $filename, string $contents ) {
		$g = new Grammar();
		$g->filename = $filename;
		return $g->parse( $contents );
	}
}

start = _ Definitions

/* Line number bookkeeping.
 * Be careful about backtracking after you successfully match this production.
 */
eol = nl:("\n" / "\r\n" / "\r") { $this->lineNum++; return $nl; }

/* WebIDL terminals */
integer = m:opt_minus n:( decimal_integer / hex_integer / octal_integer ) {
	return $m * $n;
}

opt_minus = "-" { return -1; } / "" { return 1; }

decimal_integer = s:$( [1-9] [0-9]* ) { return intval($s); }

hex_integer = ("0x" / "0X" ) s:$( [0-9A-Fa-f]+ ) { return hexdec($s); }

octal_integer = s:$( "0" [0-7]* ) { return octdec( $s ); }

decimal = s:$( m:opt_minus (
	( [0-9]+ "." [0-9]* / [0-9]* "." [0-9]+ ) ( [Ee] [+-]? [0-9]+ )? /
	[0-9]+ [Ee] [+-]? [0-9]+
) ) { return floatval( $s ); }

identifier = !(ArgumentNameKeyword / BufferRelatedType / OtherIdLike)
   s:$( [-_]? [A-Za-z] [-_0-9A-Za-z]* )
   // weird special case reserved identifiers:
   &{ return $s !== "_constructor" && $s !== "_toString" && $s !== "toString"; }
   { return $s; }

string = "\"" s:$([^\"]*) "\"" { return $s; }

whitespace = ( [\t ] / eol )+

comment = "//" [^\n\r]* / "/*" ( [^\n\r*]+ / eol / "*" !"/" )* "*/"

otherchar = !otherterminals ![()\[\]{},] [^\t\n\r 0-9A-Za-z]
		   
/* The 'ignore' production -- we have to include this after every
 * terminal not defined above */
_ = ( whitespace / comment )*
// this is used after "identifier-like" nonterminals
i_ = ![-_0-9A-Za-z] _
		   
/* WebIDL non-terminals */
Definitions = (ExtendedAttributeList Definition)*

Definition =
		   CallbackOrInterfaceOrMixin
		   / Namespace
		   / Partial
		   / Dictionary
		   / Enum
		   / Typedef
		   / IncludesStatement

ArgumentNameKeyword =
 ( "async"
 / "attribute"
 / "callback"
 / "const"
 / "constructor"
 / "deleter"
 / "dictionary"
 / "enum"
 / "getter"
 / "includes"
 / "inherit"
 / "interface"
 / "iterable"
 / "maplike"
 / "mixin"
 / "namespace"
 / "partial"
 / "readonly"
 / "required"
 / "setlike"
 / "setter"
 / "static"
 / "stringifier"
 / "typedef"
 / "unrestricted" ) i_

CallbackOrInterfaceOrMixin =
	"callback" i_ CallbackRestOrInterface
  / "interface" i_ InterfaceOrMixin
InterfaceOrMixin = InterfaceRest / MixinRest
InterfaceRest =
    identifier _ Inheritance "{" _ InterfaceMembers "}" _ ";" _
Partial = "partial" i_ PartialDefinition
PartialDefinition =
    "interface" i_ PartialInterfaceOrPartialMixin
  / PartialDictionary
  / Namespace
PartialInterfaceOrPartialMixin =
	PartialInterfaceRest
  / MixinRest
PartialInterfaceRest =
	identifier _ "{" _ PartialInterfaceMembers "}" _ ";" _
InterfaceMembers = ( ExtendedAttributeList InterfaceMember )*
InterfaceMember =
	PartialInterfaceMember
  / Constructor
PartialInterfaceMembers = ( ExtendedAttributeList PartialInterfaceMember )*
PartialInterfaceMember =
	Const
  / Operation
  / Stringifier
  / StaticMember
  / Iterable
  / AsyncIterable
  / ReadOnlyMember
  / ReadWriteAttribute
  / ReadWriteMaplike
  / ReadWriteSetlike
  / InheritAttribute
Inheritance = ( ":" _ identifier _ )?
MixinRest = "mixin" i_ identifier _ "{" _ MixinMembers "}" _ ";" _
MixinMembers = ( ExtendedAttributeList MixinMember )*
MixinMember =
	Const
  / RegularOperation
  / Stringifier
  / OptionalReadOnly AttributeRest

IncludesStatement = identifier _ "includes" i_ identifier _ ";" _
		   
CallbackRestOrInterface =
	CallbackRest
  / "interface" i_ identifier _ "{" _ CallbackInterfaceMembers "}" _ ";" _
CallbackInterfaceMembers = ( ExtendedAttributeList CallbackInterfaceMember )*
CallbackInterfaceMember =
	Const
  / RegularOperation

Const = "const" i_ ConstType identifier _ "=" _ ConstValue ";" _
ConstValue =
   BooleanLiteral
 / FloatLiteral
 / i:integer _ { return $i; }
BooleanLiteral = "true" i_ { return true; } / "false" i_ { return false; }
FloatLiteral = f:decimal _ { return $f; }
	/ "-Infinity" i_ { return -INF; }
	/ "Infinity" i_ { return INF; }
	/ "NaN" i_ { return NAN; }
ConstType = PrimitiveType
 / identifier _

ReadOnlyMember = "readonly" i_ ReadOnlyMemberRest
ReadOnlyMemberRest =
   AttributeRest
 / MaplikeRest
 / SetlikeRest
ReadWriteAttribute =
   AttributeRest
InheritAttribute = "inherit" i_ AttributeRest
AttributeRest = "attribute" i_ TypeWithExtendedAttributes AttributeName ";" _
AttributeName = AttributeNameKeyword
 / identifier _
AttributeNameKeyword = ("async" / "required") i_
OptionalReadOnly = ("readonly" i_)?
DefaultValue =
   ConstValue
 / string _
 / "[" _ "]" _
 / "{" _ "}" _
 / "null" i_

Operation =
   RegularOperation
 / SpecialOperation
RegularOperation = Type OperationRest
SpecialOperation = Special RegularOperation
Special = "getter" i_
 / "setter" i_
 / "deleter" i_
OperationRest = OptionalOperationName "(" _ ArgumentList ")" _ ";" _
OptionalOperationName = OperationName?
OperationName =
   OperationNameKeyword
 / identifier _
OperationNameKeyword = "includes" i_

ArgumentList = Argument Arguments / ""
Arguments = "," _ Argument Arguments / ""
Argument = ExtendedAttributeList ArgumentRest
ArgumentRest =
   "optional" i_ TypeWithExtendedAttributes ArgumentName Default
 / Type Ellipsis ArgumentName
ArgumentName =
   ArgumentNameKeyword
 / identifier _
Ellipsis = ("..." _)?

Constructor =
   "constructor" i_ "(" _ ArgumentList ")" _ ";" _
Stringifier =
   "stringifier" i_ StringifierRest
StringifierRest =
   OptionalReadOnly AttributeRest
 / RegularOperation
 / ";" _
StaticMember = "static" i_ StaticMemberRest
StaticMemberRest =
   OptionalReadOnly AttributeRest
 / RegularOperation
Iterable =
   "iterable" i_ "<" _ TypeWithExtendedAttributes OptionalType ">" _ ";" _
OptionalType = ( "," _ TypeWithExtendedAttributes )?
AsyncIterable =
   "async" i_ "iterable" i_ "<" _ TypeWithExtendedAttributes OptionalType ">" _ OptionalArgumentList ";" _
OptionalArgumentList =
   "(" _ ArgumentList ")" _
 / ""
ReadWriteMaplike = MaplikeRest
MaplikeRest =
   "maplike" i_ "<" _ TypeWithExtendedAttributes "," _ TypeWithExtendedAttributes ">" _ ";" _
ReadWriteSetlike = SetlikeRest
SetlikeRest =
   "setlike" i_ "<" _ TypeWithExtendedAttributes ">" _ ";" _

Namespace =
   "namespace" i_ identifier _ "{" _ NamespaceMembers "}" _ ";" _
NamespaceMembers = ( ExtendedAttributeList NamespaceMember )*
NamespaceMember =
   RegularOperation
 / "readonly" i_ AttributeRest

Dictionary =
   "dictionary" i_ identifier _ Inheritance "{" _ DictionaryMembers "}" _ ";" _
DictionaryMembers = DictionaryMember*
DictionaryMember = ExtendedAttributeList DictionaryMemberRest
DictionaryMemberRest =
   "required" i_ TypeWithExtendedAttributes identifier _ ";" _
 / Type identifier _ Default ";" _
PartialDictionary =
   "dictionary" i_ identifier _ "{" _ DictionaryMembers "}" _ ";" _
Default = ( "=" _ DefaultValue )?
Enum = "enum" i_ identifier _ "{" _ EnumValueList "}" _ ";" _
EnumValueList = string _ EnumValueListComma
EnumValueListComma = ( "," _ EnumValueListString )?
EnumValueListString = ( string _ EnumValueListComma )?
CallbackRest = identifier _ "=" _ Type "(" _ ArgumentList ")" _ ";" _
Typedef = "typedef" i_ TypeWithExtendedAttributes identifier _ ";" _

Type =
   SingleType
 / UnionType Null
TypeWithExtendedAttributes =
   ExtendedAttributeList Type
SingleType =
   DistinguishableType
 / "any" i_
 / PromiseType
UnionType = "(" _ UnionMemberType ("or" i_ UnionMemberType)+ ")" _
UnionMemberType =
   ExtendedAttributeList DistinguishableType
 / UnionType Null

DistinguishableType =
   PrimitiveType Null
 / StringType Null
 / "sequence" i_ "<" _ TypeWithExtendedAttributes ">" _ Null
 / "object" i_ Null
 / "symbol" i_ Null
 / BufferRelatedType Null
 / "FrozenArray" i_ "<" _ TypeWithExtendedAttributes ">" _ Null
 / "ObservableArray" i_ "<" _ TypeWithExtendedAttributes ">" _ Null
 / RecordType Null
 / identifier _ Null
PrimitiveType =
   UnsignedIntegerType
 / UnrestrictedFloatType
 / "undefined" i_
 / "boolean" i_
 / "byte" i_
 / "octet" i_
 / "bigint" i_
UnrestrictedFloatType =
   "unrestricted" i_ FloatType
 / FloatType
FloatType = ( "float" / "double" ) i_
UnsignedIntegerType =
   "unsigned" i_ IntegerType
 / IntegerType
IntegerType =
   "short" i_
 / "long" i_ OptionalLong
OptionalLong = ( "long" i_ )?

StringType =
   "ByteString" i_
 / "DOMString" i_
 / "USVString" i_

PromiseType =
   "Promise" i_ "<" _ Type ">" _
RecordType =
   "record" i_ "<" _ StringType "," _ TypeWithExtendedAttributes ">" _
Null = ("?" _)?

BufferRelatedType =
 ( "ArrayBuffer"
 / "DataView"
 / "Int8Array"
 / "Int16Array"
 / "Int32Array"
 / "Uint8Array"
 / "Uint16Array"
 / "Uint32Array"
 / "Uint8ClampedArray"
 / "Float32Array"
 / "Float64Array" ) i_

ExtendedAttributeList =
   "[" _ ExtendedAttribute ExtendedAttributes "]" _ / !"[" ""
ExtendedAttributes = "," _ ExtendedAttribute ExtendedAttributes / ""
ExtendedAttribute =
   "(" _ ExtendedAttributeInner ")" _ ExtendedAttributeRest
 / "[" _ ExtendedAttributeInner "]" _ ExtendedAttributeRest
 / "{" _ ExtendedAttributeInner "}" _ ExtendedAttributeRest
 / Other ExtendedAttributeRest
ExtendedAttributeRest = ExtendedAttribute?
ExtendedAttributeInner =
   "(" _ ExtendedAttributeInner ")" _ ExtendedAttributeInner
 / "[" _ ExtendedAttributeInner "]" _ ExtendedAttributeInner
 / "{" _ ExtendedAttributeInner "}" _ ExtendedAttributeInner
 / OtherOrComma ExtendedAttributeInner
 / ""
Other =
   OtherIdLike
 / ArgumentNameKeyword
 / BufferRelatedType
 / c:$(
   integer
 / decimal
 / identifier
 / string
 / otherchar
 / otherterminals ) _ { return $c; }
OtherIdLike = c:$(
   "ByteString"
 / "DOMString"
 / "FrozenArray"
 / "Infinity"
 / "NaN"
 / "ObservableArray"
 / "Promise"
 / "USVString"
 / "any"
 / "bigint"
 / "boolean"
 / "byte"
 / "double"
 / "false"
 / "float"
 / "long"
 / "null"
 / "object"
 / "octet"
 / "or"
 / "optional"
 / "record"
 / "sequence"
 / "short"
 / "symbol"
 / "true"
 / "unsigned"
 / "undefined" ) i_ { return $c; }
otherterminals =
   "-"
 / "-Infinity"
 / "."
 / "..."
 / ":"
 / ";"
 / "<"
 / "="
 / ">"
 / "?"
OtherOrComma =
   Other
 / "," _
IdentifierList = identifier _ Identifiers
Identifiers = "," _ identifier _ Identifiers / ""
ExtendedAttributeNoArgs = identifier _
ExtendedAttributeArgList = identifier _ "(" _ ArgumentList ")" _
ExtendedAttributeIdent = identifier _ "=" _ identifier _
ExtendedAttributeIdentList = identifier _ "=" _ "(" _ IdentifierList ")" _
ExtendedAttributeNamedArgList = identifier _ "=" _ identifier _ "(" _ ArgumentList ")" _
